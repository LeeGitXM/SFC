FS-reactant-change
(
    MG : integer,
    BLOCK: class s88-recipe-entity
) =
(
)
grade: integer;
new_total_polyrate: quantity;
WS: class kb-workspace = the subworkspace of this procedure;
DM, DM1: class explanation-message;
P: class ph-procedure-or-method = this procedure;
DEBUG-MODE: truth-value = false; 
ERROR: class s88-error;
ERROR-TXT: text;

begin { begin 1  }

	if the debug-mode of P or the debug-mode of BLOCK then DEBUG-MODE = true;
	if DEBUG-MODE then post "In [the name of P]. . ."; 
	begin
		new_total_polyrate = call s88-get (BLOCK, "new.poly-rate-from-user.val", s88-superior-operation);
		grade = call s88-get (BLOCK, "new.grade-number.val", s88-superior-operation);
	end
	on error (ERROR)
		delete ERROR;
		ERROR-TXT = "[the name of P] failed to collect RECIPE data for the new polymer rate and grade."; 
		call s88-post-message-to-queue(BLOCK, ERROR-TXT, em-critical, BLOCK); 
		return; 
	end;

{  Initialize the workspace and its messages. }

    for DM = each explanation-message upon WS do 
            delete DM without permanence checks;
    end; 

case ( MG ) of 
 
{  WARN THE OPERATOR WHEN H2 IS TO BE STARTED. } 
    1:
     begin   
        create a explanation-message DM1;
        change the text of DM1 to "For [grade] at [new_total_polyrate] Klb/D Hydrogen addition is required.  [CR]Please line up the Hydrogen and make up the cascades shown on DCS display VC230.  [CR]The setpoint of VCC150 should be set to 0.0.  [CR]Do NOT download this rate change until the hydrogen is able to flow to the reactor." ; 
        change the background-color of DM1 to white; 
        transfer DM1 to WS; 
        change the size of WS to minimum;
        change the background-color of WS to the symbol yellow;
        start s88-post-message-to-queue (BLOCK, "A series reactor rate change to [new_total_polyrate] for grade [grade] occurred at [the current real time as a time stamp]. Hydrogen flow was initiated.", em-information, BLOCK);
        call s88-set-data (BLOCK, "show-series-instruction.val", "true H2 1", s88-superior-operation);
      end;

{ WARN THE OPERATOR WHEN H2 IS TO BE STOPPED. } 
    2: 
     begin
        create a explanation-message DM1;
        change the text of DM1 to "For [grade] at [new_total_polyrate] Klb/D Hydrogen addition is NOT required.  [CR]After downloading this rate change please block in the Hydrogen and put VCV120 and VCV130 in manual and close both valves.  [CR]The hydrogen controls are shown on DCS display VC230."; 
        change the background-color of DM1 to white; 
        transfer DM1 to WS;
        change the size of WS to minimum; 
        change the background-color of WS to the symbol yellow;
        start s88-post-message-to-queue (BLOCK, "A series reactor rate change to [new_total_polyrate] for grade [grade] occurred at [the current real time as a time stamp]. Hydrogen flow was terminated.", em-information, BLOCK);
        call s88-set-data (BLOCK, "show-series-instruction.val", "true H2 2", s88-superior-operation); 
      end;
 
{ WARN THE OPERATOR WHEN NH3 IS TO BE STARTED. } 
 
    3:
      begin
        create a explanation-message DM1;
        change the text of DM1 to "For [grade] at [new_total_polyrate] Klb/D Ammonia addition is required.  [CR]Please line up the Ammonia and make up the cascades shown on DCS display VC330.  [CR]The setpoint of VCC260 should be set to 0.0.  [CR]Do NOT download this rate change until the Ammonia is able to flow to the reactor."; 
        change the background-color of DM1 to white;
        transfer DM1 to WS;
        change the size of WS to minimum; 
        change the background-color of WS to the symbol sky-blue;
        start s88-post-message-to-queue (BLOCK, "A series reactor rate change to [new_total_polyrate] for grade [grade] occurred at [the current real time as a time stamp].  Ammonia flow was initiated.", em-information, BLOCK);
        call s88-set-data (BLOCK, "show-series-instruction.val", "true NH3 3", s88-superior-operation); 
      end;

{ WARN THE OPERATOR WHEN NH3 IS TO BE STOPPED. } 

    4:
     begin 
        create a explanation-message DM1;
        change the text of DM1 to "For [grade] at [new_total_polyrate] Klb/D Ammonia addition is NOT required.  [CR]After down loading this rate change please block in the Ammonia and put VCF251 and VCF252 in manual and close both valves. [CR]The Ammonia controls are shown on on DCS display VC330.[CR][CR]This procedure has reduced the desired propylene by 10%."; 
        change the background-color of DM1 to white; 
        transfer DM1 to WS;
        change the size of WS to minimum; 
        change the background-color of WS to the symbol sky-blue;
        start s88-post-message-to-queue (BLOCK, "A series reactor rate change to [new_total_polyrate] for grade [grade] occurred at [the current real time as a time stamp].  Ammonia flow was terminated.", em-information, BLOCK);
        call s88-set-data (BLOCK, "show-series-instruction.val", "true NH3 4", s88-superior-operation); 
      end;

otherwise: 
    begin 
        call s88-set-data (BLOCK, "show-series-instruction.val", "false [MG]", s88-superior-operation); 
        inform the operator for the next 10 minutes that "No message for MG= [MG] has been found.";
    end;   
end;    
  
	if DEBUG-MODE then post "[the name of P] has completed."; 

end  { for begin 1  }