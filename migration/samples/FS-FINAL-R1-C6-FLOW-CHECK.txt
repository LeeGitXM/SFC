fs-final-r1-c6-flow-check (BLOCK: class s88-recipe-entity) = (symbol {complete, stopped, or error} )

{ The main hexane flow SP is set to the flying swithc target value if the current SP is within C6-flow-tolerance of the target SP otherwise an error is logged and the flying switch is terminated. }

{RDW this is the Single R1 C6 check version 1}

UNIT-PROCEDURE: class s88-unit-procedure;
STATUS: symbol;
SETPOINT-KEY: value;
Output-Data: class s88-recipe-output-data;
DESIRED-SETPOINT, CURRENT-SETPOINT, SETPOINT-TOLERANCE: float;
VAR-NAME: symbol; 
VAR: class g2-variable; 
P: class ph-procedure-or-method = this procedure;
DEBUG-MODE: truth-value = false; 

begin
	if the debug-mode of P or the debug-mode of BLOCK then DEBUG-MODE = true;

	if DEBUG-MODE then post "In [the name of P] with [the label of BLOCK], a [the class of BLOCK]. . .";

	conclude that the state of BLOCK = s88-running-state;

	UNIT-PROCEDURE = call s88-get-unit-procedure(BLOCK);

	STATUS, Output-Data = call s88-get-data (BLOCK, "new.r1-c6-sp", s88-superior-operation); 
	if STATUS /= the symbol success then begin
		call s88-post-error(UNIT-PROCEDURE, BLOCK, "Unable to locate <r1-c6-sp> in the Flying Switch Operation recipe data.");
		call s88-set-data(BLOCK, "callback-result.val", "Unable to locate <r1-c6-sp> in the flying switch operation recipe data.", s88-local); 
		return s88-complete-state; 
	end; 

	VAR-NAME = the tag of Output-Data;

	if DEBUG-MODE then post "In [the name of P] with var-name= [var-name].";
 
	if not(there exists a g2-variable VAR named by VAR-NAME) then begin
		call s88-post-error(UNIT-PROCEDURE, BLOCK, "Unable to locate an output variable named <[VAR-NAME]>");
		call s88-set-data(BLOCK, "callback-result.val", "Unable to locate an output variable named <[VAR-NAME]>", s88-local); 
		return s88-complete-state; 
	end;

	DESIRED-SETPOINT = the val of Output-Data;

	if DEBUG-MODE then post "In [the name of P] with desired-sp= [desired-setpoint]."; 

	collect data (timing out after opc-latency-time) 
		CURRENT-SETPOINT = the value of VAR; 
		if timeout then
			CURRENT-SETPOINT = 0.0;
	end; 

	if DEBUG-MODE then post "In [the name of P] with current-sp= [current-setpoint]."; 

	STATUS, SETPOINT-TOLERANCE = call s88-get-data(BLOCK, "c6-flow-tolerance.val", s88-superior-operation);
	if STATUS /= the symbol success then begin
		call s88-post-error(UNIT-PROCEDURE, BLOCK, "Unable to locate <c6-flow-tolerance.val> in the global recipe data.");
		call s88-set-data(BLOCK, "callback-result.val", "Unable to locate <c6-flow-tolerance.val> in the global recipe data.", s88-local); 
		return s88-complete-state; 
	end; 

{ We have found all of the objects required to begin. . . }

	if DEBUG-MODE then post "In [the name of P] with C6 SP[CR]Current= [CURRENT-SETPOINT][CR] Desired: [DESIRED-SETPOINT][CR]  Tolerance: [SETPOINT-TOLERANCE]";

	if (abs (DESIRED-SETPOINT - CURRENT-SETPOINT) < SETPOINT-TOLERANCE) then begin
		call s88-set-data(BLOCK, "callback-result.val", "OK to Download", s88-local);
		end
	else begin
		call s88-set-data(BLOCK, "callback-result.val", "Current SP not within tolerance", s88-local); 
	end; 

    if DEBUG-MODE then post "In [the name of P] with [the label of BLOCK] has completed normally!";

    return s88-complete-state;
end